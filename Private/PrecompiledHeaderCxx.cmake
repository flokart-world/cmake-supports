# Copyright (c) 2014-2016 Flokart World, Inc.
#
# This software is provided 'as-is', without any express or implied
# warranty. In no event will the authors be held liable for any damages
# arising from the use of this software.
# 
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
# 
#    1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 
#    2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 
#    3. This notice may not be removed or altered from any source distribution.

function (CMS_PRECOMPILE_HEADER_CXX _headerFile)
  set (_headerPath "${CMAKE_CURRENT_BINARY_DIR}/${_headerFile}")
  CMS_INCLUDE_DIRECTORIES(PRIVATE ${CMAKE_CURRENT_BINARY_DIR})

  if (MSVC AND USE_PRECOMPILED_HEADER)
    file (READ "${_headerFile}.in" _headerInput)
    string (CONFIGURE "${_headerInput}" _headerOutput @ONLY)
    CMS_WRITE_FILE(${_headerPath} ${_headerOutput})

    get_filename_component (_headerName ${_headerFile} NAME_WE)
    set (_sourcePath "${CMAKE_CURRENT_BINARY_DIR}/${_headerName}.cpp")
    set (CMS_PCH_CXX_HEADER ${_headerFile})

    CMS_WRITE_FILE(${_sourcePath}
                   "// Generated by CMakeSupports ${CMakeSupports_VERSION}.\n"
                   "// This file exists just for the precompiled header.\n")

    CMS_ADD_COMPILE_OPTIONS(PRIVATE /FI\"${_headerFile}\"
                                    /Yu\"${_headerFile}\")
    CMS_ADD_SOURCE_FILES("${_headerFile}.in" ${_sourcePath} NO_GROUP)
    CMS_APPEND_TO_SOURCE_FILE_PROPERTY(${_sourcePath}
                                       CompileOptions "/Yc\"${_headerFile}\"")

    set (CMS_PCH_CXX_SOURCE ${_sourcePath} PARENT_SCOPE)
  else ()
    CMS_WRITE_FILE(${_headerPath}
                   "// Precompiled header is disabled or not supported.\n")

    set (CMS_PCH_CXX_SOURCE "" PARENT_SCOPE)
  endif ()
endfunction ()

function (_CMS_INIT_PCH_OPTION)
  set (USE_PRECOMPILED_HEADER true CACHE BOOL "Using precompiled header improves compilation speed, but may drop some missing includes.")
  mark_as_advanced (USE_PRECOMPILED_HEADER)
endfunction ()

if (MSVC)
  _CMS_INIT_PCH_OPTION()
endif ()
